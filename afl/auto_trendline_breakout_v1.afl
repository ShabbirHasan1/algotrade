// AutoTrend v1
SetBarsRequired(sbrAll,sbrAll);
nbar             = Param("nbar", 2, 2, 10, 1);
nbarExt          = Param("extra_bars", 1, 1, 10, 1);
stopATRLen       = Optimize("stop_atr_len", 8, 1, 20, 1);
stopATRMul       = Optimize("stop_atr_mul", 2, 2, 5, 1);
showShortSignals = ParamToggle("show_short_signals", "No|Yes", 0);

function GetFractals(nbar_t)
{
    // Define conditions
    PHigh            = H > Ref(HHV(H, nbar_t), -1) AND Ref(HHV(H, nbar_t), nbar_t) <= H;
    PLow             = L < Ref(LLV(L, nbar_t), -1) AND Ref(LLV(L, nbar_t), nbar_t) >= L;

    // High Fractals (Take last 4)
    PHighPrice1      = ValueWhen(PHigh, H, 1);
    PHighPrice2      = ValueWhen(PHigh, H, 2);
    PHighPrice3      = ValueWhen(PHigh, H, 3);
    PHighPrice4      = ValueWhen(PHigh, H, 4);

    // Low Fractals (Take last 4)
    PLowPrice1       = ValueWhen(PLow, L, 1);
    PLowPrice2       = ValueWhen(PLow, L, 2);
    PLowPrice3       = ValueWhen(PLow, L, 3);
    PLowPrice4       = ValueWhen(PLow, L, 4);

    // Conditions
    BuyCond          = PHighPrice1 >= PHighPrice2 AND PHighPrice2 <= PHighPrice3 AND PHighPrice3 <= PHighPrice4;
    ShortCond        = PLowPrice1 <= PLowPrice2 AND PLowPrice2 >= PLowPrice3 AND PLowPrice3 >= PLowPrice4;
    
    VarSet("PHighPrice1", PHighPrice1);
    VarSet("PHighPrice2", PHighPrice2);
    VarSet("PHighPrice3", PHighPrice3);
    VarSet("PHighPrice4", PHighPrice4);
    
    VarSet("PLowPrice1", PLowPrice1);
    VarSet("PLowPrice2", PLowPrice2);
    VarSet("PLowPrice3", PLowPrice3);
    VarSet("PLotPrice4", PLowPrice4);
    
    VarSet("BuyCond", BuyCond);
    VarSet("ShortCond", ShortCond);
}

BuyPrimary       = False;
ShortPrimary     = False;
nbarLow          = IIf((nbar - nbarExt) < 1, 1, (nbar - nbarExt));
nbarHigh         = (nbar + nbarExt);

// Execution signals
for (i= nbarLow; i <= nbarHigh; i++)
{
    GetFractals(i);
    BuyCond          = VarGet("BuyCond");
    ShortCond        = VarGet("ShortCond");
    PHighPrice1      = VarGet("PHighPrice1");
    PLowPrice1       = VarGet("PLowPrice1");
    BuyPrimary       = BuyPrimary OR (Cross(Close, PHighPrice1) AND BuyCond);
    ShortPrimary     = ShortPrimary OR (Cross(PLowPrice1, Close) AND ShortCond);
}

Buy     = BuyPrimary;
Sell    = 0;
Short   = False;
Cover   = 0;
if (showShortSignals)
{
    Short   = ShortPrimary;
}

// Apply stops
ApplyStop(stopTypeTrailing, stopModePoint, stopATRMul*ATR(stopATRLen), True, True);

PlotShapes(IIf(Buy, shapeHollowUpTriangle, shapeNone), colorGreen, 0, Close, -50);
PlotShapes(IIf(Short, shapeHollowDownTriangle, shapeNone), colorRed, 0, Close, -50);
